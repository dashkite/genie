#!/usr/bin/env coffee
# TODO: we should actually check the arguments and have a usage message
[_,_,cssRoot,buildRoot] = process.argv
system = require "node-system"
{read,readdir,write,exists,stat} = require "fairmont"
{resolve,dirname,extname} = require "path"

system "mkdir -p #{buildRoot}"

mtime = do ->
  _mtime = (path) -> 
    if exists( path ) then stat( path ).mtime.getTime() else -1
    
  (source,destination,fn) ->
    fn() if _mtime( source ) > _mtime( destination )

for file in readdir( cssRoot )
  source = resolve( cssRoot, file )
  destination = resolve( buildRoot, file.replace(/coffee$/,"css") )
  mtime source, destination, ->
    switch extname( file )
      when ".coffee"
          console.log "Generating CSS from #{file}"
          write( destination, require( source ).main() )
      when ".css"
        console.log "Copying CSS from #{file}"
        system "cp #{source} #{destination}"

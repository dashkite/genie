#!/usr/bin/env coffee
{resolve} = require "path"
{watch} = require "chokidar"
system = require "node-system"
{argv,stderr,stdout} = process
  
write = ->
  [stream,items...] = arguments
  for item in items
    stream.write item

[_,_,path,rest...] = argv
command = rest.join(" ")
write stderr, "watching #{path}\n"

watcher = watch resolve(path), 
  ignored: /^\./, ignoreInitial: true, persistent: true

watcher
  .on( "add", (path) -> do build )
  .on( "change", (path) -> do build )
  .on( "unlink", (path) -> do build )
  .on( "error", (error) -> console.error error )

build = ->
  write stderr, "running [ #{command} ] ..."
  try 
    system command
    write stderr, " done.\n"
  catch error
    write stderr, "\n", error.stack, "\n"
  
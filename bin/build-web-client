#!/usr/bin/env coffee
{rmdir,chdir,directory,file,sh,shLocal,log,abort,run} = require "../src/tasks"

Tasks = 
  
  clean: ->
    log "Cleaning Web client build directories"
    rmdir( "build" )
    rmdir( "src/web/ark/node_modules" )
    
  npm: ->
    log "Installing Node modules for Ark"
    file "src/web/ark/node_modules", ->
      chdir "src/web/ark", ->
        sh "npm install"
    
  copy: ->
    log "Synchronizing source and build directories"
    directory "build/web"
    directory "build/ark"
    sh "rsync -avv src/web/ark/ build/ark/", -> log( "Done" )
    sh "rsync -avv src/web/assets/ build/web", -> log( "Done" )
    
  ark: ->
    log "Building the ark"
    directory "node_modules/ark", ->
      log "Installing ark"
      sh "npm install ark --save-dev"
    ark = "node_modules/ark/bin/ark"
    manifest = "build/ark/ark.cson"
    file manifest, ->
      abort "Missing ark manifest"
    file "build/ark/package.json", ->
      abort "Missing ark package.json"
    js = "build/web/js/application.js"
    directory "build/web/js"
    sh "#{ark} package -t -m #{manifest} -f #{js}"
    
  html: ->
    log "Building static Web pages"
    shLocal "bin/build-html src/web/ark/html/pages.cson build/web/"
    
  css: ->
    log "Building CSS files"
    shLocal "bin/build-css src/web/css build/web/css"
    
  config: (name) ->
    log "Bundling configuration"
    sh "rsync -avv env/#{name}/config.cson build/ark/", -> log( "Done" )
    
  all: (name) ->
    log "Building the Web client (doesn't include clean)"
    run @npm, @copy, (=> @config( name ) ), @ark, @html, @css

[_,_,name,args...] = process.argv
Tasks[name]?(args...)

